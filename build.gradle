plugins {
    id 'io.miret.etienne.sass' version '1.1.1'
}

compileSass {
    outputDir = project.file("${buildDir}/style")
    sourceDir = project.file("src/main/sass")
    style = compressed
}

task clean(type: Delete, group: 'build') {
    delete buildDir
}

task html(type: Copy, group: 'build') {
    from "$projectDir/src/main/html"
    into "$buildDir"
}

task image(type: Copy, group: 'build') {
    from "$projectDir/src/main/image"
    into "$buildDir/image"
}

task script(type: Copy, group: 'build') {
    from "$projectDir/src/main/script"
    into "$buildDir/script"
}

task style(group: 'build', dependsOn: 'compileSass')

task build(group: 'build', dependsOn: [html, image, style, 'script'])

task publish(group: 'publish', dependsOn: build) {
    doLast {
        def timestamp = Instant.now().epochSecond
        def server = 'filip@barbucha.jirsak.org'
        def targetDir = "/var/web/wud.cz/2020.$timestamp/"

        exec {
            executable 'plink'
            args server
            args 'mkdir'
            args targetDir
        }

        exec {
            executable 'pscp'
            args '-batch'
            args '-r'
            args '-p'
            args "$buildDir/"
            args "$server:$targetDir"
        }

        exec {
            executable 'plink'
            args server
            args 'cd'
            args '/var/web/wud.cz/'
            args '&&'
            args 'ln'
            args '-sbTf'
            args "2020.$timestamp/"
            args '2020'
        }
    }
}
